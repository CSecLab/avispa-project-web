role alice (A, B    : agent,
            Hash    : hash_func,
            Kprev   : symmetric_key,
            SND, RCV: channel(dy))
played_by A def=

  local State : nat,
        Na, Nb: text,
        Kb, K : symmetric_key

  init State := 0

  transition

    0.  State  = 0 /\ RCV(start) =|>
        State':= 2 /\ Na':= new() /\ SND({Na'.A}_Kprev)
                   /\ witness(A,B,bob_alice_na,Na')

    2.  State  = 2 /\ RCV({Na.Nb'.B}_Kprev) =|>
        State':= 4 /\ Kb':= Nb'.Na /\ K':= new() /\ SND({K'}_Kb')
                   /\ secret(K',k,{A,B})

    4.  State  = 4 /\ RCV(Hash(K)) =|>
        State':= 6 /\ request(A,B,alice_bob_nb,Nb)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role bob(A, B    : agent,
         Hash    : hash_func,
         Kprev   : symmetric_key,
         SND, RCV: channel(dy))
played_by B def=

  local State : nat,
        Na, Nb: text,
        Kb, K : symmetric_key

  init State := 1

  transition

    1.  State  = 1 /\ RCV({Na'.A}_Kprev) =|>
        State':= 3 /\ Nb':= new() /\ Kb':= Nb'.Na' /\ SND({Na'.Nb'.B}_Kprev)
                   /\ witness(B,A,alice_bob_nb,Nb')

    3.  State  = 3 /\ RCV({K'}_Kb) =|>
        State':= 5 /\ SND(Hash(K'))
				   /\ request(B,A,bob_alice_na,Na)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role session(A, B : agent,
			 Hash : hash_func,
             Kprev: symmetric_key)
def=

  local SA, RA, SB, RB: channel(dy)

  composition

       alice(A,B,Hash,Kprev,SA,RA)
    /\ bob  (A,B,Hash,Kprev,SB,RB)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role environment()
def=

  const a, b         : agent,
        h            : hash_func,
        kprevab,
        kprevai,
        kprevib      : symmetric_key,
        k,
        alice_bob_nb,
        bob_alice_na : protocol_id

  intruder_knowledge = {a, b, h, kprevai, kprevib}

  composition

       session(a,b,h,kprevab)
    /\ session(a,i,h,kprevai)
    /\ session(i,b,h,kprevib)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

goal

  secrecy_of k
  authentication_on alice_bob_nb
  authentication_on bob_alice_na

end goal

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

environment()
