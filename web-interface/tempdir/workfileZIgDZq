%Protokol Woo-Lam
%Zjednoduseny zapis protokolu
% A -> B : A
% B -> A : Nb
% A -> B : { Nb }Kas
% B -> S : { A, { Nb }Kas}Kbs
% S -> B : {Nb }Kbs


%Definovanie subjektu Alice

role alice (A, S, B: agent,
		Kas : symmetric_key,
		SND_BA, RCV_BA: channel(dy))
  
  played_by A def=
  
  local State : nat,
	Nb : text
	
  init State := 0
  
  transition
	1. State  = 0 /\ RCV_BA(start) =|>
	   State':= 2 /\ SND_BA(A)

	2. State  = 2 /\ RCV_BA(Nb') =|>
	   State':= 4 /\ SND_BA({Nb'}_Kas)
	              /\ witness(A,B,bob_alice_nb,Nb') 
end role


%Definovanie subjektu Bob

role bob (A, S, B: agent,
		Kbs : symmetric_key,
		SND_AB, RCV_AB,SND_SB,RCV_SB: channel(dy))
  
  played_by B def=

  local State : nat,
	Nb : text,
	X : {text}_symmetric_key

  init State := 1

  transition
	1. State  = 1 /\ RCV_AB(A) =|>
	   State':= 3 /\ Nb':= new()
                      /\ SND_AB(Nb')
	 	      
        2. State = 3 /\ RCV_AB(X') =|>
           State':=5 /\ SND_SB({A.X'}_Kbs)

        3. State = 5 /\ RCV_SB({Nb}_Kbs) =|>
           State':=7 /\ request(B,A,bob_alice_nb,Nb)
end role


%Definovanie servera

role server (A, S, B : agent,
		Kas, Kbs : symmetric_key,
		SND_BS, RCV_BS: channel(dy))
  
	played_by S def=
  
	local State : nat,
		Nb : text
	
	init State := 5

	transition
		1. State  = 5 /\ RCV_BS({A.{Nb'}_Kas}_Kbs) =|>
		   State':= 7 /\ SND_BS({Nb'}_Kbs)
end role


%Definícia zloženej role

role session(A, S, B : agent,
		Kas, Kbs : symmetric_key)
	def=
  
	local
		SSB, RSB,
		SBA, RBA,
		SBS, RBS,
		SAB, RAB : channel (dy)
 
	composition
		   alice (A, S, B, Kas, SBA, RBA)
		/\ server(A, S, B, Kas, Kbs, SBS, RBS)
		/\ bob (A, S, B, Kbs, SAB, RAB,SSB,RSB)
end role


%Definovanie najvyzsej urovne

role environment()
	def=
	const a, b, s : agent,
		kas, kbs, kis : symmetric_key,
		bob_alice_nb: protocol_id

	intruder_knowledge = {a, b, s, kis}

	composition
		   session(a,s,b,kas,kbs)
		/\ session(a,s,i,kas,kis)
		/\ session(i,s,b,kis,kbs)
end role

%Definovanie cielov

goal
	authentication_on alice_bob_nb
end goal

%??????????????????????????????????????????

environment()