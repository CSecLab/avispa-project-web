role alice (A, B    : agent,
            Hash    : hash_func,
            SND, RCV: channel(dy))
played_by A def=

  local State : nat,
        Na, Nb: text,
        Kb, K : symmetric_key

  const k, alice_bob_nb, bob_alice_na, alice_bob_k, bob_alice_k: protocol_id

  init State := 0

  transition

    0.  State  = 0 /\ RCV(start) =|>
        State':= 2 /\ Na':= new() /\ SND(Na'.A)
                   /\ witness(A,B,bob_alice_na,Na')

    2.  State  = 2 /\ RCV(Nb'.B) =|>
        State':= 4 /\ Kb':= Nb'.Na /\ K':= new() /\ SND({K'}_Kb')
                   /\ secret(K',k,{A,B})
                   /\ request(A,B,alice_bob_nb,Nb')
                   /\ witness(A,B,bob_alice_k,K')

    4.  State  = 4 /\ RCV(Hash(K)) =|>
        State':= 6 /\ request(A,B,alice_bob_k,K)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role bob(A, B    : agent,
         Hash    : hash_func,
         SND, RCV: channel(dy))
played_by B def=

  local State : nat,
        Na, Nb: text,
        Kb, K : symmetric_key

  const k, alice_bob_nb, bob_alice_na, alice_bob_k, bob_alice_k: protocol_id

  init State := 1

  transition

    1.  State  = 1 /\ RCV(Na'.A) =|>
        State':= 3 /\ Nb':= new() /\ Kb':= Nb'.Na' /\ SND(Nb'.B)
				   /\ request(B,A,bob_alice_na,Na')
                   /\ witness(B,A,alice_bob_nb,Nb')

    3.  State  = 3 /\ RCV({K'}_Kb) =|>
        State':= 5 /\ SND(Hash(K'))
                   /\ request(B,A,bob_alice_k,K')
                   /\ witness(B,A,alice_bob_k,K')

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role session(A, B: agent,
			 Hash: hash_func)
def=

  local SA, RA, SB, RB: channel(dy)

  composition

       alice(A,B,Hash,SA,RA)
    /\ bob  (A,B,Hash,SB,RB)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role environment()
def=

  const a, b         : agent,
        h            : hash_func,
        k,
        alice_bob_nb,
        bob_alice_na,
        alice_bob_k,
        bob_alice_k  : protocol_id

  intruder_knowledge = {a, b, h}

  composition

       session(a,b,h)
    /\ session(a,i,h)
    /\ session(i,b,h)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

goal

  secrecy_of k
  % authentication_on alice_bob_nb
  % authentication_on bob_alice_na

end goal

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

environment()
   