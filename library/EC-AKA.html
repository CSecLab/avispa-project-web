<BR><DT><B>PROTOCOL:</B> <DD> <BR>
	EC-AKA (Ensured Confidentiality Authentication and Key Agreement)
<P>
&nbsp;<BR><BR><DT><B>VARIANT:</B> <DD> <BR>
	EPS-AKA
<P>
&nbsp;<BR><BR><DT><B>PURPOSE:</B> <DD> <BR>
	End-to-End Confdential Authentication and key agreement in 3GPP mobile networks (LTE, LTE-A)
<P>
&nbsp;<BR><BR><DT><B>REFERENCE:</B> <DD> <BR>
 	<A HREF="http://www.airccse.org/journal/nsa/0912nsa06.pdf">http://www.airccse.org/journal/nsa/0912nsa06.pdf</A>
<P>
 	<A HREF="http://jacquesbouabdo.org/files/documents/Ensure-Confidentiality-Authentication-and-Key-Agreement-Protocol-for-EPS.pdf">http://jacquesbouabdo.org/files/documents/Ensure-Confidentiality-Authentication-and-Key-Agreement-Protocol-for-EPS.pdf</A>
 	<A HREF="http://dx.doi.org/10.1109/RELABIRA.2012.6235098">http://dx.doi.org/10.1109/RELABIRA.2012.6235098</A>
<P>
	<A HREF="http://jacquesbouabdo.org/files/documents/EPS-mutual-authentication-and-Crypt-analyzing-SPAKA.pdf">http://jacquesbouabdo.org/files/documents/EPS-mutual-authentication-and-Crypt-analyzing-SPAKA.pdf</A>	
	<A HREF="http://dx.doi.org/10.1109/ComManTel.2013.6482410">http://dx.doi.org/10.1109/ComManTel.2013.6482410</A>
<P>
	<A HREF="http://jacquesbouabdo.org/files/documents/EC-AKA2-a-revolutionary-AKA-protocol-final.pdf">http://jacquesbouabdo.org/files/documents/EC-AKA2-a-revolutionary-AKA-protocol-final.pdf</A>
	<A HREF="http://dx.doi.org/10.1109/ICCAT.2013.6521993">http://dx.doi.org/10.1109/ICCAT.2013.6521993</A>  
<P>
&nbsp;<BR><BR><DT><B>MODELER:</B> <DD> <BR>
 <UL>
	<LI> Jacques Bou Abdo, Notre Dame University, 2013
	<I>jbouabdo at ndu dot edu dot lb</I>
<P>
	<LI> Jacques Demerjian, Antonine University, 2013
	<I>jacques dot demerjian at upa dot edu dot lb</I>
<P>
	<LI> Hakima chaouchi, Telecom SudParis, 2013
	<I>hakima dot chaouchi at telecom dash sudparis dot eu</I>
  </UL>
<P>
&nbsp;<BR><BR><DT><B>ALICE_BOB:</B> <DD> <BR>
 <PRE>
 	U->M: ({IMSI.PSQN}_Pkh)						| NAS Attach Request
 	M->H: (ImsiEnc.{SNID}_Pkh)					| Authenticatin Data Request
    	H->M: ({RAND.Vf2.Vf3.Vf4.xor(SQN,Vf5).AMF.Vf1.EK}_Pkm)		| Authentication Data Reply
 	M->U: ({RAND.xor(SQN,F5(Xvf5)).AMF'.F1(Xvf1)}_EK)		| Authentication Request
    	U->M: ({RAND.xor(SQN,F5(Xvf5)).AMF.F1(Xvf1)}_EK) 		| User Authentication Response
 </PRE>
<P>
&nbsp;<BR><BR><DT><B>LIMITATIONS:</B> <DD> <BR>
	Still vulnerable against LTE-based attackeds such as UE tracking, example: Tracking user temporary ID
	These vulnerabilities are solved using EC-AKA2:
	http://jacquesbouabdo.org/files/documents/EC-AKA2-a-revolutionary-AKA-protocol-final.pdf
 	http://dx.doi.org/10.1109/ICCAT.2013.6521993
<P>
&nbsp;<BR><BR><DT><B>PROBLEMS:</B> <DD> <BR>
<UL>
<LI> strong authentication on <TT>mme_ue_vf2</TT>
<LI> strong authentication on <TT>ue_mme_vf1</TT>
<LI>secrecy of <TT>imsi</TT>
<LI>secrecy of <TT>vf2</TT>
</UL>
	4
	authentication_on mme_ue_vf2
	authentication_on ue_mme_vf1
	secrecy_of imsi
	secrecy_of vf2
&nbsp;<BR><BR><DT><B>CLASSIFICATION:</B> <DD> <BR>
	D.4.6	G.2
<P>
&nbsp;<BR><BR><DT><B>ATTACKS:</B> <DD> <BR>
 None
&nbsp;<BR><HR>
<BR><DT><B>HLPSL:</B> <DD> <BR>
<PRE>

role role_U (U,H,M: agent,              
        IMSI,PEK, PSQN, K, AMF, Vf1, Vf2, Vf3, Vf4, Vf5:text,
        F1, F2, F3,F4, F5:hash_func,
        Pkh : public_key,
        SND,RCV:channel(dy))




played_by U
def=
        local
                State:nat,
                SQN ,RAND : text,
                Xvf2 , Xvf3, Xvf4, Xvf5 : text.text,
                EK:symmetric_key,   
                Xvf1: text.text.text.text

                const ue_mme_vf1, ue_hss_vf4, mme_ue_vf2 : protocol_id

        init
                State := 0
        transition
                1. State=0 /\ RCV(start) =|> State':=1  /\ SND({IMSI.PSQN}_Pkh)  
                                                /\ secret(IMSI ,imsi, {H, U})
                                                /\ EK' := xor(PEK, PSQN')  
                
                4. State=1 /\ RCV({RAND'.xor(SQN',F5(Xvf5')).AMF'.F1(Xvf1')}_EK) =|> State':=2  
                                                                        /\SND({F2(K.RAND')}_EK)  
                                                                        /\ request (U,M, mme_ue_vf2,F2(K.RAND')) 
                                                                        /\ witness (U,M, ue_mme_vf1,Vf1') 
                                                                                
end role

<HR>

role role_M(M:agent,H:agent,U:agent,
                SNID:text,
                F1, F2, F3,F4, F5:hash_func,
                Pkh, Pkm : public_key,
                SND,RCV:channel(dy))
played_by M
def=
        local
                State:nat,IMSI:text ,AMF:text,SQN:text,K:text,RAND, Vf1, Vf2, Vf3, Vf4, Vf5:text,
                Xvf2 , Xvf3, Xvf4, Xvf5 : text.text,   
                Xvf1: text.text.text.text,
                EK:symmetric_key,
                ImsiEnc : {text}_public_key
        init
                State := 0

        transition
                1. State=0 /\ RCV(ImsiEnc) =|> State':=1 
                                                /\ SND(ImsiEnc.{SNID}_Pkh)
                
                3. State=1 /\ RCV({RAND'.F2(Xvf2').F3(Xvf3').F4(Xvf4').xor(SQN',F5(Xvf5')).AMF'.F1(Xvf1').EK'}_Pkm) =|> State':=2  
                                                                                                                        /\ SND({RAND'.xor(SQN',F5(Xvf5')).AMF'.F1(Xvf1')}_EK') 
                                                                                                                        /\ request (M,U, ue_mme_vf1,F1(Xvf1'))



                5. State=2 /\ RCV({F2(K.RAND)}_EK) =|> State':=3 /\ witness(M, U, mme_ue_vf2, F2(K'.RAND')) 
end role

<HR>

role role_H(H, M, U:agent,
        K, AMF, PEK,Vf1, Vf2, Vf3, Vf4, Vf5:text, 
        F1, F2, F3, F4, F5:hash_func,
        Pkh,Pkm : public_key,
        SND,RCV:channel(dy))
played_by H
def=
        local
                State:nat, RAND, SNID:text,SQN:text,IMSI:text,PSQN:text, EK:symmetric_key
        init
                State := 0
        transition
                2. State=0 /\ RCV({IMSI'.PSQN'}_Pkh.{SNID'}_Pkh) =|> State':=1  
                                                        /\ SQN':=new()
                                                        /\ RAND':=new()
                                                        /\ EK' :=xor(PEK, PSQN') 
                                                        /\ Vf1':=F1(K.SQN'.RAND'.AMF)
                                                        /\ Vf2':=F2(K.RAND')
                                                        /\ Vf3':=F3(K.RAND')
                                                        /\ Vf4':=F4(K.RAND')
                                                        /\ Vf5':=F5(K.RAND')
                                                        /\ SND({RAND'.Vf2'.Vf3'.Vf4'.xor(SQN',Vf5').AMF'.Vf1'.EK'}_Pkm)
                                                        /\ secret(Vf2', vf2, {U,H})     
                                                        

end role

<HR>

role session1(
        H ,M ,U:agent, 
        SNID, PEK, IMSI, PSQN, K, AMF, RAND, Vf1, Vf2, Vf3, Vf4, Vf5: text,
        F1 ,F2 ,F3 ,F4, F5: hash_func,
        Pkh, Pkm: public_key)
def=
        local
                SND3, RCV3, SND2, RCV2, SND1, RCV1 : channel(dy)
        composition
                   role_H(H, M, U, K, AMF, PEK, Vf1, Vf2, Vf3, Vf4, Vf5, F1, F2, F3, F4, F5, Pkh,Pkm , SND3, RCV3)      
                /\ role_M(M, H, U, SNID, F1, F2, F3, F4, F5, Pkh, Pkm,SND2, RCV2) 
                /\ role_U(U, H, M, IMSI, PEK, PSQN, K, AMF, Vf1, Vf2, Vf3, Vf4, Vf5, F1, F2, F3, F4, F5, Pkh , SND1, RCV1)
end role

<HR>

role environment()
def=
        const
                hash_0:hash_func,
                amf:text,
                k, ki:text,imsi:text, snid:text, psqn:text,
                mme:agent,hss:agent,ue:agent,
                f1, f2, f3,f4, f5: hash_func,
                rand:text,const_1:text,
                pek:text,
                vf1,vf2,vf3,vf4,vf5:text,
                pkh, pkm: public_key,
                auth_1, sec_2: protocol_id
        
intruder_knowledge = {ki, snid, f1, f2, f3, f4, f5, pkh, pkm}
        composition
                session1(hss, mme,ue, snid, pek,imsi,psqn,k,amf,rand,vf1,vf2,vf3,vf4,vf5,f1,f2,f3,f4,f5, pkh, pkm)

end role

<HR>
goal
        authentication_on mme_ue_vf2
        authentication_on ue_mme_vf1
        secrecy_of imsi
        secrecy_of vf2
        
end goal
<HR>
environment()
</PRE>
