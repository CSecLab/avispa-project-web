	
%%HLPSL:
role client(A, S: agent,
	    PSK: hash(agent.agent),
	    PRF,MICA,MICS: hash_func,
	    SND, RCV: channel (dy))
played_by A
def=

  local  State : nat, 
         T, Ns, Nc,ID : text,
         AK     : hash(hash(agent.agent).agent.text.text),
         AuthS  : hash(hash(hash(agent.agent).agent.text.text).text)

  const  sec_PSK,sec_AK : protocol_id

  init   State := 0

  transition 

  1. State = 0   /\ RCV(start) 
     =|>
     State' := 1 /\ SND(A)

  2. State = 1   /\ RCV(Ns'.T'.S)
     =|>
     State' := 2 /\ Nc' := new()
                 /\ AK' := PRF(PSK.S.Nc'.T')
                 /\ SND(Nc'.MICA(AK'.Ns')) 
                 /\ witness(A,S,autha,MICA(AK'.Ns')) 
                 /\ secret(PSK,sec_PSK,{A,S})
                 /\ secret(AK',sec_AK,{A,S})
  
   3. State = 2   /\ RCV(ID'.MICS(AK'.Nc'))
      =|>
      State' := 3 /\ AuthS' := MICS(AK'.Nc')
                  /\ request(S,A,auths,MICS(AK'.Nc'))
                  
end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 	

role server(A,S : agent,
            PSK: hash(agent.agent),
	    PRF,MICA,MICS: hash_func,
	    SND, RCV: channel (dy))
played_by S
def=

  local State : nat,
        T, Ns, Nc,ID : text,
        AK    : hash(hash(agent.agent).agent.text.text),
        AuthA : hash(hash(hash(agent.agent).agent.text.text).text)
  const  sec_PSK,sec_AK : protocol_id
  init  State := 0

  transition 
   1. State = 0   /\ RCV(A') 
      =|>
      State' := 1 /\ Ns' := new()
                  /\ T' := new()
                  /\ SND(Ns'.T'.S)

   2. State = 1   /\ RCV(Nc'.MICA(AK'.Ns')) 
                  
      =|>
      State' := 2 /\ AuthA' := MICA(AK'.Ns') 
                  /\ request(S,A,autha,MICA(AK'.Ns'))
                  /\ ID' := new()
                  /\ SND(ID'.MICS(AK'.Nc'))
                  /\ witness(S,A,auths,MICS(AK'.Nc')) 
                  /\ secret(PSK,sec_PSK,{A,S})
                  /\ secret(AK',sec_AK,{A,S})
                  
end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 	

role session(A, S: agent,
             PRF,MICA,MICS: hash_func)
def=

  local PSK: hash(agent.agent),
        SNDA, SNDS, RCVA, RCVS: channel (dy)

  init PSK := PRF(A.S)

  composition
       client(A,S,PSK,PRF,MICA,MICS,SNDA,RCVA)
    /\ server(A,S,PSK,PRF,MICA,MICS,SNDS,RCVS)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 	

role environment()
def=

 const a, s : agent,
       prf, mica, mics : hash_func,
       autha,auths : protocol_id

 intruder_knowledge = {a,s,i,mica,mics}

 composition
      session(a,s,prf,mica,mics)
   /\ session(i,s,prf,mica,mics)
   /\ session(a,s,prf,mica,mics)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

goal

  %secrecy_of PSK
  secrecy_of sec_PSK % Addresses G12

  %Server authenticates Client on auth
  authentication_on autha  % Addresses G1, G2, G3
  authentication_on auths
end goal

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

environment()
 