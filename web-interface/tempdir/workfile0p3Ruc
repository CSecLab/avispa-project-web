role role_RP 	(
	RP,OP,TE			:	agent,
	ClientId			:	text,
	ROKey,RTKey			:	symmetric_key,
	TEKey				:	public_key,
	AuthRequest,AuthResponse,
	TokenRequest,TokenResponse	:	protocol_id,
	SND,RCV				:	channel(dy) 
		)
played_by RP
def=
	local
		State			:	nat,
		Stat,Code,IdToken	:	text
	init
		State := 0
	transition
		1. 	State=0 /\ RCV(start) =|>
			State':=1 
			/\ Stat':=new() 
			/\ secret(Stat',sec_3,{RP,OP}) 
			/\ SND({AuthRequest.ClientId.Stat'}_ROKey)
		2. 	State=1 /\ RCV({AuthResponse.Code'.Stat'}_ROKey) =|> 
			State':=2 
			/\ secret(Stat',sec_3,{RP,OP}) 
			/\ SND({TokenRequest.Code'}_RTKey)
		3. 	State=2 /\ RCV({TokenResponse.{IdToken'}_inv(TEKey)}_RTKey) =|> 
			State':=3 
end role

role role_OP	(
	OP,RP,TE			:	agent,
	ClientId			:	text,
	ROKey				:	symmetric_key,
	AuthRequest,AuthResponse	:	protocol_id,
	SND,RCV				:	channel(dy)
		)
played_by OP
def=
	local
		State			:	nat,
		Code			:	text,
		Stat			:	text
	init
		State := 0
	transition
		1. 	State=0 /\ RCV({AuthRequest.ClientId'.Stat'}_ROKey) =|> 
			State':=1 
			/\ witness(OP,RP,auth_1,ClientId')
			/\ secret(Stat',sec_3,{RP,OP}) 
			/\ Code':=new() 
			/\ SND({AuthResponse.Code'.Stat'}_ROKey)
end role

role role_TE	(
	TE,RP,OP			:	agent,
	RTKey				:	symmetric_key,
	TEKey				:	public_key,
	TokenRequest,TokenResponse	:	protocol_id,
	SND,RCV				:	channel(dy)
		)
played_by TE
def=
	local
		State			:	nat,
		Code			:	text,
		IdToken			:	text
	init
		State := 0
	transition
		1. 	State=0 /\ RCV({TokenRequest.Code'}_RTKey) =|> 
			State':=1 
			/\ witness(TE,RP,auth_2,Code') 
			/\ IdToken':=new() 
			/\ SND({TokenResponse.{IdToken'}_inv(TEKey)}_RTKey)
end role

role session 	(
	RP,OP,TE			:	agent,
	ClientId			:	text,
	ROKey,RTKey			:	symmetric_key,
	TEKey				:	public_key,
	AuthRequest,AuthResponse,
	TokenRequest,TokenResponse	:	protocol_id
		)
def=
	local
		SND1,RCV1,SND2,RCV2,SND3,RCV3 :	channel(dy)
	composition
		role_RP(RP,OP,TE,ClientId,ROKey,RTKey,TEKey,AuthRequest,AuthResponse,TokenRequest,TokenResponse,SND1,RCV1) 
	     /\ role_OP(OP,RP,TE,ClientId,ROKey,AuthRequest,AuthResponse,SND2,RCV2) 
	     /\ role_TE(TE,RP,OP,RTKey,TEKey,TokenRequest,TokenResponse,SND3,RCV3)
end role

role environment()
def=
	const
		rp,op,te		:	agent,
		clientid		:	text,
		rokey,rtkey		:	symmetric_key,
		tekey			:	public_key,
		authrequest,authresponse,	
		tokenrequest,tokentesponse	:	protocol_id,
		auth_1				:	protocol_id,
		auth_2				:	protocol_id,
		sec_3				:	protocol_id
	intruder_knowledge = {rp,op,te,tekey}
	composition
		session(rp,op,te,clientid,rokey,rtkey,tekey,authrequest,authresponse,tokenrequest,tokentesponse)		
	      /\ session(i,op,te,clientid,rokey,rtkey,tekey,authrequest,authresponse,tokenrequest,tokentesponse)
	      /\ session(rp,i,te,clientid,rokey,rtkey,tekey,authrequest,authresponse,tokenrequest,tokentesponse)
	      /\ session(rp,op,i,clientid,rokey,rtkey,tekey,authrequest,authresponse,tokenrequest,tokentesponse)
 
		
end role

goal
	authentication_on auth_1
	authentication_on auth_2
	secrecy_of sec_3
end goal

environment()
