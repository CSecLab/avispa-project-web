%% a->b: {Na}_Pkb
%% b->a: {Na,Nb}_Pka
%% a->b {Nb}_Pkb

role alice ( A,B : agent,
                 Pka, Pkb : public_key,
                 Snd, Rcv : channel(dy))
played_by A 
def=

 local  State: nat,
        Na, Nb   : text
        
 init  State := 0

 transition

   1. State = 0
      /\ Rcv(start)
      =|> 
      State' := 2 /\ Na' := new() 
      /\ Snd({A.B.Na'}_Pkb)
      /\ witness(A,B,b_a_na,Na')
      /\ secret(Na',na,{A,B})
  
   2. State = 2 /\ Rcv({A.B.Na'.Nb'}_Pka) 
      =|> 
      State' := 4 /\ Snd({B.Nb'}_Pkb)
      /\ request(A,B,a_b_nb,Nb)
      

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

role bob ( A,B : agent,
                 Pka, Pkb : public_key,
                 Snd, Rcv : channel(dy))
played_by B 

def=

 local  State: nat,
        Na, Nb   : text
        
 init  State := 1

 transition

   1. State = 1 /\ Rcv({A.B.Na'}_Pkb) =|> 
      State' := 3 /\ Nb' := new() /\ Snd({A.B.Na'.Nb'}_Pka)
      /\ witness(B,A,a_b_nb,Nb')
      /\ secret(Nb',nb,{A,B})
  
   2. State = 3 /\ Rcv({B.Nb'}_Pkb) =|> 
      State' := 5 
      /\ request(B,A,b_a_na,Na)
      

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

role session (A, B : agent,
	      Pka  : public_key,
              Pkb  : public_key)
def=

  local SA, RA, SB, RB: channel (dy)
  composition 

	alice(A,B,Pka,Pkb,SA,RA)
     /\ bob  (A,B,Pka,Pkb,SB,RB)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

role environment() def=

    const a, b	       : agent,
	  ka, kb, ki   : public_key,
	  na, nb,
	  a_b_nb,
	  b_a_na : protocol_id

    intruder_knowledge = {a, b, ka, kb, ki, inv(ki)}

    composition

	session(a,b,ka,kb)
     /\ session(a,i,ka,ki)
     /\ session(i,b,ki,kb)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

goal

  secrecy_of na, nb
  authentication_on a_b_nb
  authentication_on b_a_na


end goal

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 

environment()
