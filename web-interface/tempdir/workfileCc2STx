%%HLPSL:
%reader
role lwAKE_Init (A,B: agent,
               K1,K2: symmetric_key,
	       TID : text,
               Snd,Rcv: channel(dy))
played_by A
def=

  local State   : nat,
        Na,Nb   : text

  const sec_k1 : protocol_id

  init  State := 0

  transition

   1. State = 0
      /\ Rcv(start)
      =|> 
      State' := 1
      /\ Na' := new()
      /\ Snd(xor(Na',K1).{xor(TID,Na')}_K2) 

   2. State = 1
      /\ Rcv({xor(K2,Nb')}_TID.{xor(xor(K1,Na'),Nb')}_K2.Na')
      =|> 
      State' := 2
      /\ secret(K1,sec_k1,{A,B})
      /\ witness(A,B,na,Na')
      /\ request(A,B,nb,Nb')

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%tag
role lwAKE_Resp (B,A: agent,
               K1,K2: symmetric_key,
	       TID : text,
 	       Snd,Rcv: channel(dy))
played_by B
def=

  local State   : nat,
        Na,Nb   : text,

  const sec_k2 : protocol_id

  init  State := 0

  transition

   1. State = 0 /\ Rcv(xor(Na',K1).{xor(TID,Na')}_K2)
      =|> 
      State' := 1
      /\ Nb' := new()
      /\ Snd({xor(K2,Nb')}_TID.{xor(xor(K1,Na'),Nb')}_K2.Na')
      /\ secret(K2,sec_k2,{A,B})
      /\ witness(B,A,nb,Nb')
      /\ request(B,A,na,Na)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role session(A,B: agent,
	     K1,K2: symmetric_key,
	     TID: text)
def=

  local SA, RA, SB, RB: channel (dy)

  composition
     lwAKE_Init(A,B,K1,K2,TID,SA,RA)
  /\ lwAKE_Resp(B,A,K1,K2,TID,SB,RB)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


role environment()
def=

  const a, b   : agent,
        k1,k2    : symmetric_key,
        tid  : text, 
        na, nb : protocol_id

  intruder_knowledge={a,b}

  composition
      session(a,b,k1,k2,tid)
   /\ session(b,a,k1,k2,tid)
 
end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

goal

 % Confidentiality (G12)
 secrecy_of sec_k1, sec_k2

 % Message authentication (G2)
 % lwAKE_Init authenticates lwAKE_Resp on nb
 authentication_on nb

 % Message authentication (G2)
 % lwAKE_Resp authenticates lwAKE_Init on na
 authentication_on na

end goal

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

environment()
