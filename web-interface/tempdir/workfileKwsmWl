role user(
		U, H, B			: agent,
		Ku, Kh, Kb		: public_key,
		Hash			: hash_func,
		U2h, H2u, U2b, B2u	: channel(dy)
	)
	played_by U
	def=
	local
		State			: nat,
		Req_host		: {{text.text}_inv(public_key)}_public_key,
		Req_board		: {{text.text}_inv(public_key)}_public_key,
		Req_all			: {{text.text}_inv(public_key)}_public_key.{{text.text}_inv(public_key)}_public_key,
		Hashed_req		: hash_func,
		UHOA, Nuh, Nuhb, Nub	: text
	init
		State := 1
	transition
	1.	State =       1 /\ B2u(start) =|>
		State' :=     3	/\ UHOA' := new()
				/\ Nuh' := new()
				/\ Nuhb' := new()
				/\ Nub' := new()
				/\ secret(UHOA', uhoa, {U, H})
				/\ secret(Nuh', nuh, {U, H})
				/\ secret(Nub', nub, {U, B})
				/\ secret(Nuhb', nuhb, {U, B})			%%% UHB
				/\ witness(U,B,user_board_nuhb,Nuhb')
				/\ Req_host' := {{UHOA'.Nuh'}_inv(Ku)}_Kh
				/\ Req_board' := {{Nuhb'.Nub'}_inv(Ku)}_Kb
				/\ Req_all' := Req_host'.Req_board'
				/\ U2b(Req_all')
	3.	State =       3 /\ B2u({Hashed_req'}_inv(Kb)) =|>
		State' :=     4 /\ request(U,B,board_user_reqsign,Hashed_req')		%% 4!!
				/\ witness(U,U,user_user_reqsign,Hash(Req_all))
				/\ request(U,U,user_user_reqsign,Hashed_req')
				/\ U2h({{Nuh.Nuhb}_inv(Ku)}_Kh)

end role

role board(
		U, H, B			: agent,
		Ku, Kh, Kb		: public_key,
		Hash			: hash_func,
		B2h, H2b, U2b, B2u	: channel(dy)
	)
	played_by B
	def=
	local
		State			: nat,
		Req_host		: {{text.text}_inv(public_key)}_public_key,
		Req_board		: {{text.text}_inv(public_key)}_public_key,
		Req_all			: {{text.text}_inv(public_key)}_public_key.{{text.text}_inv(public_key)}_public_key,
		Hashed_req		: hash_func,
		Nub, Nhb, M		: text,
		Nuhb_u, Nuhb_h		: text,
		Res_user		: {{text.text}_inv(public_key)}_public_key,
		Res_board		: {{text.text.text}_inv(public_key)}_public_key,
		Res_all			: {{text.text}_inv(public_key)}_public_key.{{text.text.text}_inv(public_key)}_public_key,
		Sig			: {hash_func}_inv(public_key)
	init
		State := 2
	transition
	2.	State =       2 /\ U2b(Req_host'.{{Nuhb_u'.Nub'}_inv(Ku)}_Kb) =|>
		State' :=     5	/\ Req_all' := Req_host'.{{Nuhb_u'.Nub'}_inv(Ku)}_Kb
				/\ Hashed_req' := Hash(Req_all')
				/\ witness(B,U,board_user_reqsign,Hashed_req')
				/\ witness(B,H,board_host_reqsign,Hashed_req')
				/\ Sig' := {Hashed_req'}_inv(Kb)
				/\ B2u(Sig')
				/\ B2h(Req_all'.Sig')
	5.	State =       5 /\ H2b(Res_user'.{{M'.Nuhb_h'.Nhb'}_inv(Kh)}_Kb.{Nuhb_h'}_inv(Kh)) =|>			%%  _?
		State' :=     8 /\ Res_board' := {{M'.Nuhb_h'.Nhb'}_inv(Kh)}_Kb
				/\ Res_all' := Res_user'.Res_board'
				/\ request(B,U,user_board_nuhb,Nuhb_h')					%%% 8 !!!
end role

role host(
		U, H, B			: agent,
		Ku, Kh, Kb		: public_key,
		Hash			: hash_func,
		B2h, H2b, U2h, H2u	: channel(dy)
	)
	played_by H
	def=
	local
		State			: nat,
		Req_host		: {{text.text}_inv(public_key)}_public_key,
		Req_board		: {{text.text}_inv(public_key)}_public_key,
		Req_all			: {{text.text}_inv(public_key)}_public_key.{{text.text}_inv(public_key)}_public_key,
		Hashed_req		: hash_func,
		Res_user		: {{text.text}_inv(public_key)}_public_key,
		Res_board		: {{text.text.text}_inv(public_key)}_public_key,
		Res_all			: {{text.text}_inv(public_key)}_public_key.{{text.text.text}_inv(public_key)}_public_key,
		UHOA, Token, Mask	: text,
		Nuh_u, Nuh_b, Nuhb, Nhb	: text
		
	init
		State := 4
	transition
	4.	State =       4 /\ B2h({{UHOA'.Nuh_u'}_inv(Ku)}_Kh.Req_board.{Hashed_req'}_inv(Kb)) /\ U2h({{Nuh_b'.Nuhb'}_inv(Ku)}_Kh) =|>
		State' :=     6 /\ Req_all' := {{UHOA'.Nuh_u'}_inv(Ku)}_Kh.Req_board			%%%% 6 !!!!1
				/\ request(H,B,board_host_reqsign,Hashed_req')
				/\ witness(H,H,host_host_reqsign,Hash(Req_all'))
				/\ request(H,H,host_host_reqsign,Hashed_req')
				/\ witness(H,H,host_host_nuh,Nuh_u')
				/\ request(H,H,host_host_nuh,Nuh_b')
				/\ Token' := new()
				/\ Mask' := new()
				/\ Nhb' := new()
				/\ secret(Token', token, {H, U})
				/\ secret(Mask', mask, {H, B})			%%%%% UHB
				/\ secret(Nhb', nhb, {H, B})
%%%%				/\ witness(H,U,host_user_token,Token')
				/\ Res_user' := {{xor(Token',Mask').Nuh_u'}_inv(Kh)}_Ku
				/\ Res_board' := {{Mask'.Nuhb'.Nhb'}_inv(Kh)}_Kb
				/\ Res_all' := Res_user'.Res_board'.{Nuhb'}_inv(Kh)			%%%%%%%%%%%%%%%%%
				/\ H2b(Res_all')
end role

role session(
		U, H, B				: agent,
		Ku, Kh, Kb			: public_key,
		Hash				: hash_func
	)
	def=
	local
		U2b, B2u, U2h, H2u, H2b, B2h	: channel(dy)		
	composition
		user(U, H, B, Ku, Kh, Kb, Hash, U2h, H2u, U2b, B2u) /\
		board(U, H, B, Ku, Kh, Kb, Hash, B2h, H2b, U2b, B2u) /\
		host(U, H, B, Ku, Kh, Kb, Hash, B2h, H2b, U2h, H2u)
end role

role environment()
	def=	
	const
		u, h, b, i		:	agent,
		hf			:	hash_func,
		ku, kh, kb, ki		:	public_key,
		uhoa			:	protocol_id,
		nuh			:	protocol_id,
		nub			:	protocol_id,
		nhb			:	protocol_id,
		nuhb			:	protocol_id,
		token			:	protocol_id,
		mask			:	protocol_id,
		board_user_reqsign	:	protocol_id,
		board_host_reqsign	:	protocol_id,
		user_user_reqsign	:	protocol_id,
		host_host_reqsign	:	protocol_id,
		user_board_nuhb		:	protocol_id,
		host_host_nuh		:	protocol_id
	intruder_knowledge	=	{u, h, b, i, hf, ku, kh, kb, ki}
	composition
		session(u, h, b, ku, kh, kb, hf) /\
		session(u, i, b, ku, ki, kb, hf) /\
		session(u, h, i, ku, kh, ki, hf) /\
		session(i, h, b, ki, kh, kb, hf)
end role

goal
	secrecy_of uhoa
	secrecy_of nuh
	secrecy_of nub
	secrecy_of nhb
	secrecy_of nuhb
end goal


environment()