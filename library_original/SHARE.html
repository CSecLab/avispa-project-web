<BR><DT><B>PROTOCOL:</B> <DD> SHARE<BR>
 SHARE enables two principals to obtain a shared key, assuming that
 initially each knows the public key of the other.  
<P>
&nbsp;<BR><BR><DT><B>PURPOSE:</B> <DD> <BR>
   Key establishment protocol
&nbsp;<BR><BR><DT><B>REFERENCE:</B> <DD> <BR>
   Martin Abadi, Two Facets of Authentication<BR>
   Technical Report, Digital Systems Research Centre,<BR>
   March 18, 1998
&nbsp;<BR><BR><DT><B>MODELER:</B> <DD> <BR>
   Haykal Tej, Siemens CT IC 3, 2003 and<BR>
   Luca Compagna et al, AI-Lab DIST University of Genova, November 2004
<P>
&nbsp;<BR><BR><DT><B>ALICE_BOB:</B> <DD> <BR>
 <PRE>
   1. A -> B: {Na}_Kb
   2. B -> A: {Nb}_Ka
   3. A -> B: {zero,Msg}_(Na,Nb)
   4. B -> A: {one,Msg}_(Na,Nb)
 </PRE>
&nbsp;<BR><BR><DT><B>PROBLEMS:</B> <DD> 3<BR>
<UL>
<LI>secrecy of <TT>nanb</TT>
<LI> weak authentication on <TT>k1</TT>
<LI> weak authentication on <TT>k2</TT>
</UL>
&nbsp;<BR><BR><DT><B>ATTACKS:</B> <DD> <BR>
   The responder B believes to talk with the initiator A, while it is
   talking to the intruder. The attack trace looks like:
   <PRE>
   i       -> (a,6)    :  start
   (a,6)   -> i        :  {na(a,6)}ki
   i       -> (b,4)    :  {na(a,6)}kb
   (b,4)   -> i        :  {nb(b,4)}ka
   i       -> (a,6)    :  {nb(b,4)}ka
   (a,6)   -> i        :  {zero,msg(a,6)}(na(a,6),nb(b,4))
   i       -> (b,4)    :  {zero,msg(a,6)}(na(a,6),nb(b,4))
   (b,4)   -> i        :  {one,msg(a,6)}(na(a,6),nb(b,4))
   </PRE>
<P>
&nbsp;<BR><BR><DT><B>NOTES:</B> <DD> <BR>
   Such a protocol exploits the compound types feature by simply
   imposing that the variable {\texttt{K}} can be only instantiated with a
   pair of nonces.
<P>
<P>
&nbsp;<BR><HR>
<BR><DT><B>HLPSL:</B> <DD> <BR>
<PRE>
role share_Init ( A, B     : agent, 
                  Ka, Kb   : public_key,
                  Snd, Rcv : channel(dy)) played_by A def=

  local  State   : nat,
         Na, Msg : text,
         Nb      : text,
         K       : text.text

  init   State := 0
  accept State = 3

  transition

   1. State  = 0 /\ Rcv(start) =|> 
      State':= 1 /\ Na':= new()
                 /\ Snd({Na'}_Kb)

   2. State  = 1 /\ Rcv({Nb'}_Ka) =|> 
      State':= 2 /\ Msg':= new()
                 /\ Snd({zero.Msg'}_(Na.Nb')) 
                 /\ K':= Na.Nb'
                 /\ secret(Na.Nb',nanb,{A,B})
                 /\ witness(A,B,k2,Na.Nb')

   3. State  = 2 /\ Rcv({one.Msg}_K) =|> 
      State':= 3 /\ wrequest(A,B,k1,K)

end role

<HR>

role share_Resp (B, A     : agent,
                 Kb, Ka   : public_key,
                 Snd, Rcv : channel (dy)) played_by B def=

  local State   : nat,
        Nb      : text,
        Msg, Na : text,
        K       : text.text

  init   State := 0
  accept State = 2 

  transition

   1. State  = 0 /\ Rcv({Na'}_Kb) =|> 
      State':= 1 /\ Nb':= new()
                 /\ Snd({Nb'}_Ka)
                 /\ K':= Na'.Nb'
                 /\ witness(B,A,k1,Na'.Nb')
                 /\ secret(Na'.Nb',nanb,{A,B})

   2. State  = 1 /\ Rcv({zero.Msg'}_K) =|> 
      State':= 2 /\ Snd({one.Msg'}_K)
                 /\ wrequest(B,A,k2,K)

end role

<HR>

role session(A, B           : agent,
             Ka, Kb         : public_key) def=

  local  SA, RA, SB, RB : channel (dy)

  composition
    share_Init(A,B,Ka,Kb,SA,RA) /\
    share_Resp(B,A,Kb,Ka,SB,RB)

end role

<HR>

role environment() def=
 
 const zero, one    : text,
       a, b, i      : agent,
       ka, kb, ki   : public_key,
       k1, k2, nanb : protocol_id

 intruder_knowledge = {a,b,ka,kb,ki,i,inv(ki),zero,one}

 composition

        session(a,b,ka,kb) 
    /\  session(a,i,ka,ki) 

end role

<HR>

goal


    secrecy_of nanb


    weak_authentication_on k1


    weak_authentication_on k2

end goal

<HR>

environment()
</PRE>
