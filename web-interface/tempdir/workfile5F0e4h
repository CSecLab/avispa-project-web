role smart_meter(A,B,C		:agent,				%A:Smart meter, B:IDS Sensor, C:Control center
		SK		:symmetric_key,			%session key
		H		:hash_func,			%One-way hash function
		Ppub		:public_key,			%public key of Control center
		G		:text,				%base of ECC
		SND,RCV		:channel(dy))			
played_by A
def=
	local 
		State		:nat,				%Natural number
		MIDi		:text,				%ID of Smart meter
		Ra		:text,				%Random variable
		D,O,SMi,K,W2	:text,
		Y,SIDi,F,IDcs	:text
	const a_b,b_a,a_c,subs1,
	subs2,aa_bb		:protocol_id
	init State:=0
	transition
1.	State=0	 	/\	RCV(Ppub')=|>			%get public key of control center: Ppub=g^Pr
	State':=1	/\	Ra':=new()			%a
			/\	O':=exp(Ppub',Ra)		%(g^Pr)^a=(g^a)^Pr=D^Pr
			/\	D':=exp(G,Ra)			%g^a
			/\	SMi':=xor(MIDi,H(O))		%masked ID
			/\	K':=H(MIDi.O.D')	
			/\	witness(A,C,a_c,K')		%K is used to authenticate smart meter
			/\	secret({Ra,O,MIDi},subs1,A)	%these variables are kept safe	
			/\	SND(SMi'.K'.D)		
2.	State = 1	/\	RCV(SIDi'.F'.Y'.IDcs')=|>
	State':=2	/\	W2':=H(IDcs'.O.MIDi)
			/\	SK':=exp(F',Ra)			%generate session key
			/\	secret({SK},subs2,{A,B})
			/\	Y':=H(MIDi.SIDi.SK.F.W2)	
			/\	witness(A,B,a_b,Y')		%Y is used to authenticate control center
			/\	request(A,B,aa_bb,SK)		%authenticate Session key with IDS sensor
end role
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
role ids_sensor(A,B,C		:agent,			
		Sj,SK		:symmetric_key,			%shared key between sensor and control center
		H		:hash_func,		
		Ppub		:public_key,		
		G		:text,
		SND,RCV		:channel(dy))
played_by B
def=
	local 
		State		:nat,			
		SIDi		:text,				%ID of IDS Sensor
		Rb		:text,				%Random variable
		F,E,X,SMi,K,D,W1:text,
		W2,IDcs,MIDi,Y	:text
	const a_b,b_c,c_b,b_a,bb_aa,
	subs3,subs4		:protocol_id
	init State:=0
	transition
1.	State=0	 	/\	RCV(Ppub')			%get public key of control center
			/\	RCV(SMi'.K'.D)=|>				
	State':=1	/\	Rb':=new()			%b
			/\	E':=exp(Ppub',Rb)		%Ppub^b=(g^Pr)^b=(g^b)^Pr=F^Pr
			/\	F':=exp(G,Rb)			%g^b
			/\	secret({Rb,E},subs3,B)		%these variables are kept safe
			/\	X':=H(SIDi.E.F.SMi'.D.K'.Sj)
			/\	witness(B,C,b_c,X')		%X is used to authenticate IDS sensor
			/\	SND({SIDi.F.X'.SMi'.D.K'}_Sj)
2.	State=1		/\	RCV({W1.W2.IDcs.MIDi}_Sj)=|>
	State':=2	/\	W1':=H(IDcs.E.SIDi.Sj)
			/\	witness(B,C,c_b,W1')		%W1 is used to authenticate Control center
			/\	SK':=exp(D,Rb)
			/\	secret({SK},subs4,{B,A})
			/\	Y':=H(MIDi.SIDi.SK.F.W2)
			/\	SND(SIDi.F.Y'.IDcs)
			/\	witness(B,A,b_a,Y')		%Y is used to authenticate smart meter
			/\	request(B,A,bb_aa,SK)		%authenticate Session key with smart meter
	
end role
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
role control_center(A,B,C	:agent,				%A:Smart meter, B:IDS Sensor, C:Control center
		Sj		:symmetric_key,			%session key
		H		:hash_func,			%One-way hash function
		Ppub		:public_key,			%public key of Control center
		G		:text,
		SND,RCV		:channel(dy))
played_by C
def=
	local 
		State		:nat,				%Natural number
		IDcs		:text,				%ID of Control center
		Pr		:text,				%Private key of control center
		D,O,SMi,K,W1,W2	:text,	
		X,SIDi,E,F,MIDi	:text
	const b_c,c_b,c_a,
	subs5	:protocol_id
	init State:=0
	transition
1.	State=0	 	/\	RCV(start)=|>				%Start
	State':=1	/\	Ppub':=exp(G,Pr)			%g^Pr
			/\	SND(Ppub')
2.	State=1		/\	RCV({SIDi'.F'.X'.SMi'.D'.K'}_Sj)=|>				
	State':=2	/\	O':=exp(D',Pr)				%(g^a)^Pr
			/\	E':=exp(F',Pr)				%(g^b)^Pr
			/\	X':=H(SIDi'.E'.F'.SMi'.D'.K'.Sj)	%X is used to authenticate IDS sensor		
			/\	witness(C,B,c_b,X')
			/\	MIDi':=xor(SMi',H(O'))
			/\	K':=H(MIDi'.O'.D')
			/\	witness(C,A,c_a,K')			%K is used to authenticate smart meter
			/\	secret({Pr,O',MIDi',E'},subs5,C)
			/\	W1':=H(IDcs.E'.SIDi'.Sj)	
			/\	W2':=H(IDcs.O'.MIDi')
			/\	SND({W1.W2.IDcs.MIDi}_Sj)
			/\	witness(C,B,c_b,W1')			%W1 is used to authenticate Control center

				
end role
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
role session(A,B,C		:agent,
		SK,Sj		:symmetric_key,
		H		:hash_func,	
		Ppub		:public_key,	
		G		:text)
def=
	local
		SA,SB,SC,RA,RB,RC:channel(dy)
	composition
		smart_meter(A,B,C,SK,H,Ppub,G,SA,RA)	
	/\	ids_sensor(A,B,C,sk,Sj,H,Ppub,G,SB,RB)	
	/\	control_center(A,B,C,Sj,H,Ppub,G,SC,RC)
end role
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
role environment()
def=
	const	a_b, b_c, c_b, b_a,	
		subs1,subs2,subs3,
		subs4,subs5		:protocol_id,
		a,b,c			:agent,
		sk,sj			:symmetric_key,
		kia,kai,kbi,kib,kci,kic	:symmetric_key,
		h			:hash_func,
		ppub			:public_key,
		g			:text

	intruder_knowledge={a,b,c,ppub,kia,kai,kbi,kib,kci,kic}
	composition
		session(a,b,c,sk,sj,h,ppub,g)
end role
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
goal
	secrecy_of	subs1,subs2,subs3,subs4,subs5

	authentication_on a_b
	authentication_on b_c
	authentication_on c_b
	authentication_on b_a
	authentication_on a_c
	authentication_on c_a
	authentication_on bb_aa
	authentication_on aa_bb
end goal
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

environment()