
<BR><DT><B>VARIANT:</B> <DD> EKE2 (with mutual authentication)<BR>
<BR><DT><B>PURPOSE:</B> <DD> Encrypted key exchange with mutual authentication<BR>
&nbsp;<BR><BR><DT><B>REFERENCE:</B> <DD> <BR>
 <A HREF="http://citeseer.ist.psu.edu/bellare00authenticated.html">http://citeseer.ist.psu.edu/bellare00authenticated.html</A>
&nbsp;<BR><BR><DT><B>MODELER:</B> <DD> <BR>
 <UL>
  <LI> Haykal Tej, Siemens CT IC 3, 2003
  <LI> Sebastian M&ouml;dersheim, ETH Z&uuml;rich, December 2003
 </UL>
<P>
&nbsp;<BR><BR><DT><B>ALICE_BOB:</B> <DD> <BR>
 <PRE>
 1. A -> B : A.{exp(g,X)}_K(A,B)
<P>
     B computes master key MK
     MK = H(A,B,exp(g,X),exp(g,Y),exp(g,XY))
<P>
 2. B -> A : {exp(g,Y)}_K(A,B), H(MK,1)
<P>
      A computes master key MK
<P>
 3. A -> B : H(MK,2)
<P>
      Session key K = H(MK,0)
<P>
 H : hash function
 K(A,B): password (shared key)
 </PRE>
<P>
&nbsp;<BR><BR><DT><B>LIMITATIONS:</B> <DD> None<BR>
<P>
&nbsp;<BR><BR><DT><B>PROBLEMS:</B> <DD> 3<BR>
<UL>
<LI>secrecy of <TT>sec_i_MK_A, sec_r_MK_B</TT>
<LI> strong authentication on <TT>mk_a</TT>
<LI> strong authentication on <TT>mk_b</TT>
</UL>
<P>
&nbsp;<BR><BR><DT><B>CLASSIFICATION:</B> <DD> G2 G12<BR>
<P>
&nbsp;<BR><BR><DT><B>ATTACKS:</B> <DD> None<BR>
<P>
&nbsp;<BR><BR><DT><B>NOTES:</B> <DD> For information, this protocol is an example of  <BR>
 the proposition done in 
 <A HREF="http://citeseer.ist.psu.edu/bellare00authenticated.html">http://citeseer.ist.psu.edu/bellare00authenticated.html</A> showing that
 any secure AKE (Authentication Key Exchange) protocol can be easily 
 improved to also provide MA (Mutual Authentication).
&nbsp;<BR><HR>
<BR><DT><B>HLPSL:</B> <DD> <BR>
<PRE>

role eke2_Init (A,B : agent,
                G: text,
                H: hash_func,
                Kab : symmetric_key,
                Snd,Rcv: channel(dy))
played_by A
def=

  local State     : nat, 
        X         : text, 
        GY        : message,
        MK_A,MK_B : message

  const two : text,
        sec_i_MK_A : protocol_id

  init  State := 0

  transition

   1. State = 0  /\ Rcv(start) =|> 
      State':= 1 /\ X' := new()
                 /\ Snd(A.{exp(G,X')}_Kab) 

   2. State = 1  /\ Rcv({GY'}_Kab.H(H(A.B.exp(G,X).GY'.exp(GY',X)).one)) =|> 
      State':= 2 /\ MK_A' := A.B.exp(G,X).GY'.exp(GY',X)
                 /\ MK_B' := MK_A'% Message authentication (G2)
                 /\ Snd(H(H(MK_A').two)) 
                 /\ secret(MK_A',sec_i_MK_A,{A,B})
                 /\ request(A,B,mk_a,MK_A')
                 /\ witness(A,B,mk_b,MK_B') 
end role

<HR>

role eke2_Resp (B,A : agent,
                G: text,
                H: hash_func,
                Kab : symmetric_key,
                Snd,Rcv : channel(dy))
played_by B
def=% Message authentication (G2)

  local State     : nat, 
        Y         : text,
        GX        : message,
        MK_A,MK_B : message

  const one : text,
        sec_r_MK_B : protocol_id

  init  State := 0

  transition

   1. State = 0  /\ Rcv(A.{GX'}_Kab) =|> 
      State':= 1 /\ Y' := new()
                 /\ MK_B' := A.B.GX'.exp(G,Y').exp(GX',Y')
                 /\ MK_A' := MK_B'
                 /\ Snd({exp(G,Y')}_Kab.H(H(MK_B').one))
                 /\ secret(MK_B',sec_r_MK_B,{A,B})% Message authentication (G2)
                 /\ witness(B,A,mk_a,MK_A')

   2. State = 1  /\ Rcv(H(H(MK_B).two)) =|> 
      State':= 2 /\ request(B,A,mk_b,MK_B)

end role

<HR>

role session (A,B: agent,
              G: text,
              H: hash_func,
              Kab: symmetric_key) def=

   local   SA,RA,SB,RB: channel(dy)

   composition

           eke2_Init(A,B,G,H,Kab,SA,RB)  /\
           eke2_Resp(B,A,G,H,Kab,SB,RA)

end role



role environment() def=

  const mk_a, mk_b  : protocol_id,
        a,b,c       : agent,
        kab,kai,kib : symmetric_key,
        g           : text,
        h           : hash_func

  intruder_knowledge  = {a,b,c,kai,kib}

  composition

        session(a,b,g,h,kab) /\
        session(a,i,g,h,kai) /\
        session(i,b,g,h,kib)

end role

<HR>

goal

  % Confidentiality (G12)
  % secrecy_of MK
  secrecy_of sec_i_MK_A, sec_r_MK_B

  % Message authentication (G2)
  % Eke2_Init authenticates Eke2_Resp on mk_a
  authentication_on mk_a

  % Message authentication (G2)
  % Eke2_Resp authenticates Eke2_Init on mk_b
  authentication_on mk_b

end goal

<HR>

environment()
</PRE>
