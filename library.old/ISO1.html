<BR><DT><B>PROTOCOL:</B> <DD> ISO1 Public Key Unilateral Authentication Protocol<BR>
<BR><DT><B>VARIANT:</B> <DD> one-pass unilateral authentication<BR>
<BR><DT><B>PURPOSE:</B> <DD> A client authenticates himself to a server by sending a digital signature.<BR>
&nbsp;<BR><BR><DT><B>REFERENCE:</B> <DD> <BR>
 <UL>
 <LI> [clark97survey,ISO-IEC-9798-3]
 </UL>
&nbsp;<BR><BR><DT><B>MODELER:</B> <DD> <BR>
 <UL>
 <LI> Haykal Tej, Siemens CT IC 3, 2003 and
 <LI> Luca Compagna et al, AI-Lab DIST University of Genova, November 2004
 </UL>
&nbsp;<BR><BR><DT><B>ALICE_BOB:</B> <DD> <BR>
 <PRE>
   1. A -> B : {PKa,A}inv(PKs), Na, B, Text,{Na,B,Text}inv(PKa)
 </PRE>
&nbsp;<BR><BR><DT><B>PROBLEMS:</B> <DD> 1<BR>
<UL>
<LI> strong authentication on <TT>na</TT>
</UL>
&nbsp;<BR><BR><DT><B>CLASSIFICATION:</B> <DD> G1, G2<BR>
&nbsp;<BR><BR><DT><B>ATTACKS:</B> <DD> <BR>
 The intruder can attack this protocol by simple eavesdropping
 and replaying the digital signatures.
 <PRE>
 i     -> (a,6) : start
 (a,6) -> i     : pka,a,{pka,a}inv(pks),na(a,6),b,ctext,
                  {na(a,6),b,ctext}inv(pka)
 i     -> (b,4) : pka,a,{pka,a}inv(pks),na(a,6),b,ctext,
                  {na(a,6),b,ctext}inv(pka)
 i     -> (b,7) : pka,a,{pka,a}inv(pks),na(a,6),b,ctext,
                  {na(a,6),b,ctext}inv(pka)
 </PRE>
&nbsp;<BR><BR><DT><B>NOTES:</B> <DD> <BR>
<P>
 <TT>inv(PKs)</TT> is the private key of the server <TT>C</TT>; 
 <TT>{PKa,A}inv(PKs)</TT> is the certificate of agent <TT>A</TT>.<BR>
<P>
 If one would like to use the same server public key for each
 session, then permutation on <TT>Pks</TT> should be avoided.
<P>
&nbsp;<BR><HR>
<BR><DT><B>HLPSL:</B> <DD> <BR>
<PRE>
role iso1_Init ( A,B : agent,
                 Pka, Pks : public_key,
                 Snd, Rcv : channel(dy))
played_by A
def=

 local  State: nat,
        Na   : text

 init  State := 0



 transition

   1. State = 0
      /\ Rcv(start)
      =|> 
      State' := 1
      /\ Na' := new()
      /\ Snd(Pka.A.{Pka.A}_inv(Pks).Na'.B.ctext.{Na'.B.ctext}_inv(Pka))
      /\ witness(A,B,na,Na')

end role

<HR>

role iso1_Resp (A, B: agent,
                Pks : public_key,
                Rec : channel(dy))
played_by B
def=

  local  State     : nat, 
         Pka       : public_key,
         Na, Text  : text

  init  State := 0



  transition

   1. State = 0 
      /\ Rec(Pka'.A.{Pka'.A}_inv(Pks).Na'.B.Text'.{Na'.B.Text'}_inv(Pka'))
      =|>  
      State' := 1
      /\ request(B,A,na,Na')


end role

<HR>

role session (A, B : agent,
              Pka  : public_key,
              Pks  : public_key) def=

  local SA, RA, RB: channel (dy)

  const na : protocol_id

  composition

          iso1_Init(A,B,Pka,Pks,SA,RA)
       /\ iso1_Resp(A,B,Pks,RB)

end role

<HR>

role environment() def=

  const ctext    : text,
        a, b     : agent,
        pka, pks : public_key

  intruder_knowledge={a,b,pks,pka}

  composition

        session(a,b,pka,pks)
     /\ session(a,b,pka,pks)

end role

<HR>

goal

   %ISO1_Resp authenticates ISO1_Init on na
   authentication_on na % addressess G1 and G2

end goal

<HR>

environment()
</PRE>
